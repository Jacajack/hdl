use crate::parser::ast::{ Expression, BinaryOpcode, RangeOpcode, UnaryOpcode,
	 TypeSpecifier, TypeQualifier, TypeDeclarator, TypeName, MatchExpressionStatement,
	 MatchExpressionAntecendent,TopDefinition,ModuleDeclarationStatement,VariableBlock,
	 VariableBlockStatement, VariableDeclaration, DirectDeclarator,ModuleImplementationStatement,
	 PortBindStatement,DirectInitializer,VariableDefinition,AssignmentOpcode,Root};
use crate::lexer::*;
use crate::parser::*; 
use crate::SourceSpan;
grammar(ctx: & mut ParserContext);
pub Izulu: Root={
	<start: @L> <definitions:NonEmptySpace<TopDef>> <end: @R> =>
		Root{
			definitions,
			location:SourceSpan::new_between(start,end),
		}
}
TopDef: TopDefinition={
	<start: @L> "module" <id:"Id"> "{" <statements:Space<ModuleDeclStmt>> RBrace <end: @R> =>
		TopDefinition::ModuleDeclaration{
			id,
			statements,
			location:SourceSpan::new_between(start,end),
		},
	<start: @L> "impl" <id:"Id">  <statement:ModuleImplBlockStmt>  <end: @R> =>
		TopDefinition::ModuleImplementation{
			id,
			statement,
			location:SourceSpan::new_between(start,end),
		},
	}
ModuleImplStmt: Box<ModuleImplementationStatement> = {
	<start: @L>  <block:VarBlock> <end: @R> =>
		Box::new(
			ModuleImplementationStatement::VariableBlock{
		   		block,
		   		location:SourceSpan::new_between(start,end),
	    	}
		),
	<start: @L>  <definition:VarDef> Semicolon <end: @R> =>
	    Box::new(
		    ModuleImplementationStatement::VariableDefinitionStatement{
				definition,
				location:SourceSpan::new_between(start,end),
	    	}
		),
	<start: @L>  <lhs:Expr> <assignment_opcode:AssignmentOP> <rhs:ExprWithTuple> Semicolon <end: @R> =>
		Box::new(
			ModuleImplementationStatement::AssignmentStatement{
				lhs,
				assignment_opcode,
				rhs,
				location:SourceSpan::new_between(start,end),
	    	}
	  	),
	
	<start: @L> "for" "(" <id:"Id"> "in" <range:RangeExpr> ")" <statement:ModuleImplBlockStmt> <end: @R> =>
		Box::new(
			ModuleImplementationStatement::IterationStatement{
				id,
				range,
				statement,
				location:SourceSpan::new_between(start,end),
			}
		),
	<start: @L> <id1:"Id"> <id2:"Id"> LBrace <port_bind:NonEmptyOptComma<PortBindStmt>> RBrace Semicolon <end: @R> =>
		Box::new(
			ModuleImplementationStatement::InstantiationStatement{
				id1,
				id2,
				port_bind,
				location:SourceSpan::new_between(start,end),
			}
		),
		ModuleImplBlockStmt,	
		IfStmt,			
}
ModuleImplBlockStmt: Box<ModuleImplementationStatement> ={
	<start: @L>  LBrace <statements:NonEmptySpace<ModuleImplStmt>> "}" <end: @R> =>
		Box::new(
			ModuleImplementationStatement::ModuleImplementationBlockStatement{
				statements,
				location:SourceSpan::new_between(start,end),
			}
		),
	<start: @L>  LBrace  "}" <end: @R> =>
		Box::new(
			ModuleImplementationStatement::ModuleImplementationBlockStatement{
				statements:vec![],
				location:SourceSpan::new_between(start,end),
			}
		),
}
IfStmt: Box<ModuleImplementationStatement> ={
	
	<start: @L> "if"  "(" <condition:Expr> ")" <if_statement:ModuleImplBlockStmt> "else" <else_statement:ModuleImplBlockStmt> <end: @R> =>
		Box::new(
			ModuleImplementationStatement::IfElseStatement{
				condition,
				if_statement,
				else_statement,
				location:SourceSpan::new_between(start,end),
	    	}
		),
	<start: @L> "if"  "(" <condition:Expr> ")" <if_statement:ModuleImplBlockStmt> "else" <else_statement:IfStmt> <end: @R> =>
		Box::new(
			ModuleImplementationStatement::IfElseStatement{
				condition,
				if_statement,
				else_statement,
				location:SourceSpan::new_between(start,end),
	    	}
		),
	<start: @L> "if" "(" <condition:Expr> ")"  <if_statement:ModuleImplBlockStmt> <end: @R> =>
		Box::new(
			ModuleImplementationStatement::IfStatement{
				condition,
				if_statement,
				location:SourceSpan::new_between(start,end),
	    	}
		),
}
pub PortBindStmt: PortBindStatement={
	<start:@L> <id:"Id"> <end:@R> =>
		PortBindStatement::OnlyId{
			id,
			location:SourceSpan::new_between(start,end),
		},
	<start:@L> <id:"Id"> ":" <expression:Expr> <end:@R> =>
		PortBindStatement::IdWithExpression{
			id,
			expression,
			location:SourceSpan::new_between(start,end),
		},
	<start:@L> <id:"Id"> ":" <declaration:SingleVarDecl> <end:@R> =>
		PortBindStatement::IdWithDeclaration{
			id,
			declaration,
			location:SourceSpan::new_between(start,end),
		},
}
VarDef: VariableDefinition={
	<start: @L>  <type_declarator:TypeDeclarator> <initializer_list:NonEmptyComma<DirectInitializer>> <end: @R> =>
	VariableDefinition{
		type_declarator,
		initializer_list,
		location:SourceSpan::new_between(start,end),
	}
}
DirectInitializer: DirectInitializer = {
	<start: @L>  <declarator:DirectDecl> <end: @R> =>
	DirectInitializer::DirectDeclarator{
		declarator,
		location:SourceSpan::new_between(start,end),
	},
	<start: @L>  <declarator:DirectDecl> "=" <expression:ExprWithTuple> <end: @R> =>
	DirectInitializer::DirectDeclaratorWithInitializer{
		declarator,
		expression,
		location:SourceSpan::new_between(start,end),
	},
}
ModuleDeclStmt: ModuleDeclarationStatement={
	<start: @L>  <declaration:VarDecl> Semicolon <end: @R> =>
 		ModuleDeclarationStatement::VariableDeclarationStatement{
			declaration,
			location:SourceSpan::new_between(start,end),
		},
	<start: @L>  <block:VarBlock> <end: @R> =>
 		ModuleDeclarationStatement::VariableBlock{
			block,
			location:SourceSpan::new_between(start,end),
		},
	
}
VarBlock: Box<VariableBlock>={
	<start: @L>  <types:NonEmptySpace<TypeQualifier>> LBrace <statements:Space<VarBlockStmt>> RBrace Semicolon <end: @R> =>
		Box::new(VariableBlock{
			types,
			statements,
			location:SourceSpan::new_between(start,end),
		}),
}
VarBlockStmt: VariableBlockStatement={
	<start: @L>  <block:VarBlock> <end: @R> =>
		VariableBlockStatement::VariableBlock{
	   		block,
	   		location:SourceSpan::new_between(start,end),
		},
	<start: @L>  <declaration:VarDecl> Semicolon <end: @R> =>
		VariableBlockStatement::VariableDeclarationStatement{
			declaration,
	   		location:SourceSpan::new_between(start,end),
		},
}

VarDecl: VariableDeclaration = {
	<start: @L>  <type_declarator:TypeDeclarator> <direct_declarators:NonEmptyComma<DirectDecl>> <end: @R>=>
		VariableDeclaration{
			type_declarator,
			direct_declarators,
			location:SourceSpan::new_between(start,end),
		},
}
SingleVarDecl: VariableDeclaration = {
	<start: @L>  <type_declarator:TypeDeclarator> <direct_declarator:DirectDecl> <end: @R>=>
		VariableDeclaration{
			type_declarator,
			direct_declarators: vec![direct_declarator],
			location:SourceSpan::new_between(start,end),
		},
}
DirectDecl: Box<DirectDeclarator> = {
	<start: @L>  <name:"Id"> <array_declarators:Space<IndexExpr>> <end: @R>=>
		Box::new(
			DirectDeclarator{
				name,
				array_declarators,
				location:SourceSpan::new_between(start,end),
		})
}

MatchExpressionStatement: MatchExpressionStatement = {
	<start: @L> <antecedent:MatchExpressionAntecendent> "=>" <expression:Expr> <end: @R> =>
		MatchExpressionStatement{
			antecedent,
			expression,
			location:SourceSpan::new_between(start,end),
		}
}
MatchExpressionAntecendent: MatchExpressionAntecendent = {
	<start: @L> <expression:Expr> <end: @R> => MatchExpressionAntecendent::Expression{
		expression,
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "default" <end:@R> =>MatchExpressionAntecendent::Default{
		location:SourceSpan::new_between(start,end),
	},
}
Expression: Box<Expression> = {
	<start:@L> <key:"NumericConstant"> <end:@R> => 
		Box::new(
			Expression::Number{
				key,
				location:SourceSpan::new_between(start,end)
			}
		),
	<start:@L> <id:"Id"> <end:@R> => 
		Box::new(
			Expression::Identifier{
				id,
				location:SourceSpan::new_between(start,end),
			}
		),
	<start:@L> "(" <expression:Expr> ")" <end:@R> => 
		Box::new(
			Expression::ParenthesizedExpression{
				expression,
				location:SourceSpan::new_between(start,end)
			}
		),
	
	<start:@L> "match" "(" <value:Expr> ")" LBrace <statements:NonEmptyOptComma<MatchExpressionStatement>> RBrace <end:@R> =>
		Box::new(
			Expression::MatchExpression{
				value,
				statements,
				location:SourceSpan::new_between(start,end),
			}
		),
	<start:@L> "conditional" LBrace <statements:NonEmptyOptComma<MatchExpressionStatement>> RBrace <end:@R> =>
		Box::new(
			Expression::ConditionalExpression{
				statements,
				location:SourceSpan::new_between(start,end),
			}
		),
}
ExprWithTuple:Box<Expression>={
	TupleExpr,
	Expr,
}
TupleExpr: Box<Expression> ={
	<start:@L> LBrace <expressions:OptComma<Expr>> RBrace <end:@R> =>
		Box::new(
			Expression::Tuple{
				expressions,
				location:SourceSpan::new_between(start,end)
			}
		),
}
pub RangeExpr :  Box<Expression> = {
	<start:@L> "[" <lhs:Expr> <code:RangeOp> <rhs:Expr> "]" <end:@R> =>
		Box::new(
			Expression::RangeExpression{
				lhs,
				rhs,
				code,
				location:SourceSpan::new_between(start,end)
		}
	),
}
IndexExpr : Box<Expression> ={
	"[" <PostfixExpr> "]"
}
PostfixExpr: Box<Expression>={
	<start:@L> <expression:PostfixExpr> "." <id:"Id"> <end:@R> =>
		Box::new(
			Expression::PostfixWithId{
				expression,
				id,
				location:SourceSpan::new_between(start,end),
		}
	),
	<start:@L> <expression:PostfixExpr> <index:IndexExpr> <end:@R> =>
		Box::new(
			Expression::PostfixWithIndex{
				expression,
				index,
				location:SourceSpan::new_between(start,end),
			}
		),
	<start:@L> <expression:PostfixExpr> <range:RangeExpr> <end:@R> =>
		Box::new(
			Expression::PostfixWithRange{
				expression,
				range,
				location:SourceSpan::new_between(start,end),
			}
		),
	<start:@L> <expression:PostfixExpr> "(" <args:ArgumentList ?> ")" <end:@R> =>
		match args{
			None => Box::new(
				Expression::PostfixEmptyCall{
					expression,
					location:SourceSpan::new_between(start,end),
				}
			),
			Some(argument_list) =>Box::new(
				Expression::PostfixWithArgs{
					expression,
					argument_list,
					location:SourceSpan::new_between(start,end),
				}
			),
		},		
	Expression,
}
UnaryExpr: Box<Expression> ={
	<start:@L> <code:UnaryExprOP> <expression:PostfixExpr>  <end:@R> => 
	Box::new(
		Expression::UnaryOperatorExpression{
			expression,
			code,
			location:SourceSpan::new_between(start,end),
		}
	),
	<start:@L> "(" <type_name:TypeName> ")" <expression:PostfixExpr>  <end:@R> => 
	Box::new(
		Expression::UnaryCastExpression{
			type_name,
			expression,
			location:SourceSpan::new_between(start,end),
		}
	),
	PostfixExpr,
}
Factor = Tier<FactorOp, UnaryExpr>;
Sum = Tier<AddOp, Factor>;
Shift = Tier<ShiftOp,Sum>;
BitwiseAnd = Tier<BitwiseAndOp,Shift>;
BitwiseXor = Tier<BitwiseXorOp,BitwiseAnd>;
BitwiseOr= Tier<BitwiseOrOp,BitwiseXor>;
Comparison = Tier<RelationalOp,BitwiseOr>;
Equality = Tier<EqualityOp,Comparison>;
LogicalAnd = Tier<LAandOP,Equality>;
LogicalOr = Tier<LOrOp,LogicalAnd>;// a || b
TernaryExpression : Box<Expression> = {
	<start:@L> <condition:LogicalOr> "?" <true_branch:TernaryExpression>  ":" <false_branch:TernaryExpression> <end:@R> => 
		Box::new(
			Expression::TernaryExpression{
				condition,
				true_branch,
				false_branch,
				location:SourceSpan::new_between(start,end),
			}
		),
	LogicalOr
}

pub Expr=TernaryExpression;
Tier<Op,NextTier>: Box<Expression> = {
    <start:@L> <lhs:Tier<Op,NextTier>> <code:Op> <rhs:NextTier> <end:@R> => 
	Box::new(
		Expression::BinaryExpression{
			lhs,
			rhs,
			code,
			location:SourceSpan::new_between(start,end),
		}
	),
    NextTier
};
ArgumentList=NonEmptyComma<Expr>;
NonEmptyComma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T> => match v.len() { // (2)
        0 => vec![e],
        _ => {
            v.push(e);
            v
        }
    }
};
OptComma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
NonEmptyOptComma<T>: Vec<T> = { // (1)
    <b:T> <mut v:("," <T>)*> ","? => match v.len() { // (2)
        0 => {
			v.push(b);
			v
		}
        _  => {
            v.splice(0..0, [b]);
            v
        }
    }
};
Space<T>: Vec<T> = { // (1)
    <v:(<T>)*> => v
};
NonEmptySpace<T>: Vec<T> = { // (1)
    <v:(<T>)+> => v
};
RangeOp: RangeOpcode={
	":"  => RangeOpcode::Colon,
	"+:" => RangeOpcode::PlusColon,
}
ShiftOp: BinaryOpcode={
	">>" => BinaryOpcode::RShift,
	"<<" => BinaryOpcode::LShift,
}
BitwiseAndOp: BinaryOpcode={
	"&" => BinaryOpcode::BitwiseAnd
}
BitwiseXorOp: BinaryOpcode={
	"^" => BinaryOpcode::BitwiseXor
}
BitwiseOrOp:BinaryOpcode={
	"|" => BinaryOpcode::BitwiseOr
}
RelationalOp: BinaryOpcode={
	">" => BinaryOpcode::Greater,
	"<" => BinaryOpcode::Less,
	"<=" => BinaryOpcode::LessEqual,
	">=" => BinaryOpcode::GreaterEqual
}
EqualityOp: BinaryOpcode = {
	"==" => BinaryOpcode::Equal,
	"!=" => BinaryOpcode::NotEqual,
}
LAandOP: BinaryOpcode= {
	"&&" => BinaryOpcode::LogicalAnd
}
LOrOp: BinaryOpcode ={
	"||" => BinaryOpcode::LogicalOr
}
AddOp: BinaryOpcode = { // (3)
    "+" => BinaryOpcode::Addition,
    "-" => BinaryOpcode::Substraction,
};
FactorOp: BinaryOpcode = {
    "*" => BinaryOpcode::Multiplication,
	"%" => BinaryOpcode::Modulo,
    "/" => BinaryOpcode::Division,
};
UnaryExprOP :UnaryOpcode={
	"~" => UnaryOpcode::BitwiseNot,
	"!" => UnaryOpcode::LogicalNot,
	"+" => UnaryOpcode::Plus,
	"-" => UnaryOpcode::Minus,
}
AssignmentOP: AssignmentOpcode={
	"="  => AssignmentOpcode::Equal,
	"+=" => AssignmentOpcode::PlusEqual,
	"&=" => AssignmentOpcode::AndEqual,
	"^=" => AssignmentOpcode::XorEqual,
	"|=" => AssignmentOpcode::OrEqual,
}
TypeSpecifier: TypeSpecifier = {
	<start:@L> "auto" <end:@R> => TypeSpecifier::Auto{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "int" <end:@R> => TypeSpecifier::Int{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "wire" <end:@R> => TypeSpecifier::Wire{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "bool" <end:@R> => TypeSpecifier::Bool{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "bus" "<" <width:Expression> ">" <end:@R> => TypeSpecifier::Bus{
		width,
		location:SourceSpan::new_between(start,end),
	},
}
TypeQualifier: TypeQualifier = {
	<start:@L> "signed" <end:@R> => TypeQualifier::Signed{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "unsigned" <end:@R> => TypeQualifier::Unsigned{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "tristate" <end:@R> => TypeQualifier::Tristate{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "const" <end:@R> => TypeQualifier::Const{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "input" <end:@R> => TypeQualifier::Input{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "output" <end:@R> => TypeQualifier::Output{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "async" <end:@R> => TypeQualifier::Async{
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "comb" "(" <expression:Expr> ")" <end:@R> => TypeQualifier::Comb{
		expression,
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "sync" "(" <expression:Expr> ")" <end:@R> => TypeQualifier::Sync{
		expression,
		location:SourceSpan::new_between(start,end),
	},
	<start:@L> "clock" <end:@R> => TypeQualifier::Clock{
		location:SourceSpan::new_between(start,end),
	},
	
}
TypeDeclarator: TypeDeclarator ={
	<start:@L> <qualifiers:Space<TypeQualifier>> <specifier:TypeSpecifier> <end:@R> => TypeDeclarator{
		specifier,
		qualifiers,
		location:SourceSpan::new_between(start,end),
	}
}
TypeName: TypeName ={
	<start:@L> <declarator:TypeDeclarator> <array_declarators:Space<IndexExpr>> <end:@R> => TypeName{
		declarator,
		array_declarators,
		location:SourceSpan::new_between(start,end),
	}
}
RBrace = {
	"}",
	<start:@L> ! <end:@R>  =>{
		ctx.diagnostic_buffer.push_diagnostic(ParserError{
			kind: ParserErrorKind::MissingToken(TokenKind::Punctuator(PunctuatorKind::RBrace)),
			range: SourceSpan::new_between(start,end),
		}.to_diagnostic());
		//println!("Missing Semicolon"); // for debugging
		TokenKind::Punctuator(PunctuatorKind::RBrace)
	}
}
LBrace = {
	"{",
	<start:@L> ! <end:@R>  =>{
		ctx.diagnostic_buffer.push_diagnostic(ParserError{
			kind: ParserErrorKind::MissingToken(TokenKind::Punctuator(PunctuatorKind::LBrace)),
			range: SourceSpan::new_between(start,end),
		}.to_diagnostic());
		//println!("Missing Semicolon"); // for debugging
		TokenKind::Punctuator(PunctuatorKind::LBrace)
	}
 }
Semicolon = {
	 ";" ,
	 <start:@L> ! <end:@R>  =>{
		ctx.diagnostic_buffer.push_diagnostic(ParserError{
			kind: ParserErrorKind::MissingToken(TokenKind::Punctuator(PunctuatorKind::Semicolon)),
			range: SourceSpan::new_between(start,end),
		}.to_diagnostic());
		//println!("Missing Semicolon"); // for debugging
		TokenKind::Punctuator(PunctuatorKind::Semicolon)
	}
}
extern {
    type Location = usize;
    type Error = LexerError;
  
    enum TokenKind {
		"NumericConstant" => TokenKind::NumericConstant(<NumericConstantTableKey>),
		"Id"              => TokenKind::Id(<IdTableKey>),
		"module"          => TokenKind::Keyword(KeywordKind::Module),
		"register"        => TokenKind::Keyword(KeywordKind::Register),
		"input"           => TokenKind::Keyword(KeywordKind::Input),
		"output"          => TokenKind::Keyword(KeywordKind::Output),
		"wire"            => TokenKind::Keyword(KeywordKind::Wire),
		"sync"            => TokenKind::Keyword(KeywordKind::Sync),
		"async"           => TokenKind::Keyword(KeywordKind::Async),
		"clock"           => TokenKind::Keyword(KeywordKind::Clock),
		"impl"            => TokenKind::Keyword(KeywordKind::Impl),
		"conditional"     => TokenKind::Keyword(KeywordKind::Conditional),
		"match"           => TokenKind::Keyword(KeywordKind::Match),
		"bus"             => TokenKind::Keyword(KeywordKind::Bus),
		"comb"            => TokenKind::Keyword(KeywordKind::Comb),
		"tristate"        => TokenKind::Keyword(KeywordKind::Tristate),
		"int"             => TokenKind::Keyword(KeywordKind::Int),
		"signed"          => TokenKind::Keyword(KeywordKind::Signed),
		"unsigned"        => TokenKind::Keyword(KeywordKind::Unsigned),
		"default"         => TokenKind::Keyword(KeywordKind::Default),
		"auto"            => TokenKind::Keyword(KeywordKind::Auto),
		"unused"          => TokenKind::Keyword(KeywordKind::Unused),
		"const"           => TokenKind::Keyword(KeywordKind::Const),
		"ff_sync"         => TokenKind::Keyword(KeywordKind::FfSync),
		"clock_gate"      => TokenKind::Keyword(KeywordKind::ClockGate),
		"tristate_buffer" => TokenKind::Keyword(KeywordKind::TristateBuffer),
		"enum"            => TokenKind::Keyword(KeywordKind::Enum),
		"if"              => TokenKind::Keyword(KeywordKind::If),
		"else"            => TokenKind::Keyword(KeywordKind::Else),
		"in"              => TokenKind::Keyword(KeywordKind::In),
		"for"             => TokenKind::Keyword(KeywordKind::For),
		"bool"            => TokenKind::Keyword(KeywordKind::Bool),
		"("               => TokenKind::Punctuator(PunctuatorKind::LPar),
		")"               => TokenKind::Punctuator(PunctuatorKind::RPar),
		"{"               => TokenKind::Punctuator(PunctuatorKind::LBrace),
		"}"               => TokenKind::Punctuator(PunctuatorKind::RBrace),
		"["               => TokenKind::Punctuator(PunctuatorKind::LBracket),
		"]"               => TokenKind::Punctuator(PunctuatorKind::RBracket),
		";"               => TokenKind::Punctuator(PunctuatorKind::Semicolon),
		"+"               => TokenKind::Punctuator(PunctuatorKind::Plus),
		"-"               => TokenKind::Punctuator(PunctuatorKind::Minus),
		"!"               => TokenKind::Punctuator(PunctuatorKind::LogicalNot),
		"~"               => TokenKind::Punctuator(PunctuatorKind::BitwiseNot),
		"/"               => TokenKind::Punctuator(PunctuatorKind::Slash),
		"*"               => TokenKind::Punctuator(PunctuatorKind::Asterisk),
		"="               => TokenKind::Punctuator(PunctuatorKind::Assignment),
		"+="              => TokenKind::Punctuator(PunctuatorKind::AssignmentPlus),
		"&="              => TokenKind::Punctuator(PunctuatorKind::AssignmentAnd),
		"^="              => TokenKind::Punctuator(PunctuatorKind::AssignmentXor),
		"|="              => TokenKind::Punctuator(PunctuatorKind::AssignmentOr),
		"%"               => TokenKind::Punctuator(PunctuatorKind::Modulo),
		","				  => TokenKind::Punctuator(PunctuatorKind::Comma),
		"."				  => TokenKind::Punctuator(PunctuatorKind::Dot),
		"|"				  => TokenKind::Punctuator(PunctuatorKind::BitwiseOr),
		"^"				  => TokenKind::Punctuator(PunctuatorKind::BitwiseXor),
		"&"				  => TokenKind::Punctuator(PunctuatorKind::BitwiseAnd),
		">"				  => TokenKind::Punctuator(PunctuatorKind::Greater),
		"<"				  => TokenKind::Punctuator(PunctuatorKind::Less),
		"?"				  => TokenKind::Punctuator(PunctuatorKind::QuestionMark),
		":"				  => TokenKind::Punctuator(PunctuatorKind::Colon),
		">>"			  => TokenKind::Punctuator(PunctuatorKind::RShift),
		"<<"			  => TokenKind::Punctuator(PunctuatorKind::LShift),
		"<="			  => TokenKind::Punctuator(PunctuatorKind::LessEqual),
		">="			  => TokenKind::Punctuator(PunctuatorKind::GreaterEqual),
		"=="			  => TokenKind::Punctuator(PunctuatorKind::Equals),
		"!="			  => TokenKind::Punctuator(PunctuatorKind::NotEquals),
		"&&"			  => TokenKind::Punctuator(PunctuatorKind::LogicalAnd),
		"||"			  => TokenKind::Punctuator(PunctuatorKind::LogicalOr),
		"+:"			  => TokenKind::Punctuator(PunctuatorKind::PlusColon),
		"=>"			  => TokenKind::Punctuator(PunctuatorKind::Implies),




    }
  }